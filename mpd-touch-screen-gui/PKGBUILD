# Maintainer: Moritz Bruder <muesli4 at gmail dot com>
pkgname=mpd-touch-screen-gui-git
pkgver=r143.2a4bbab
pkgrel=1
pkgdesc='Simple configurable MPD client for (touch and) LCD screens'
arch=('x86_64' 'i686' 'armv7h')
url='http://github.com/muesli/mpd-touch-screen-gui'
makedepends=('git' 'automake' 'autoconf' 'make' 'boost' 'gcc')
depends=('libmpdclient' 'sdl2' 'sdl2_image' 'sdl2_ttf' 'boost-libs' 'icu' 'libwtk-sdl2' 'libconfig')
conflicts=('mpd-touch-screen-gui')
provides=('mpd-touch-screen-gui')
source=($pkgname::git://github.com/muesli4/mpd-touch-screen-gui.git)
md5sums=(SKIP)

pkgver() {
    cd "$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$pkgname"

    autoreconf --install
    ./configure --prefix=/usr
    make
}

package() {
    cd "$pkgname"
    make DESTDIR="$pkgdir" install
}
